plugins {
	id("org.gradle.idea")
	alias(libs.plugins.detekt) apply false
	alias(libs.plugins.kotlin) apply false
}

idea {
	module {
		def excludes = [
				"temp",
		]
		excludeDirs.addAll(excludes.collect { new File(rootDir, it) })
	}
}

def detektReportMergeSarif =
		tasks.register("detektReportMergeSarif", dev.detekt.gradle.report.ReportMergeTask) {
			output = project.layout.buildDirectory.file("reports/detekt/merge.sarif")
		}

subprojects {
	plugins.withId("dev.detekt") {

		tasks.check.configure {
			dependsOn(tasks.withType(dev.detekt.gradle.Detekt))
		}
		tasks.detekt.configure { enabled = false }

		tasks.withType(dev.detekt.gradle.Detekt).configureEach {
			reports.sarif.required = true
			finalizedBy(detektReportMergeSarif)
		}
		detektReportMergeSarif.configure {
			input.from(tasks.withType(dev.detekt.gradle.Detekt).collect { it.reports.sarif.outputLocation })
		}
	}
}
